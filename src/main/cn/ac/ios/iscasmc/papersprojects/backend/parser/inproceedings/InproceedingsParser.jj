/*******************************************************************************
 * Copyright (C) 2017 Andrea Turrini
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 *******************************************************************************/
options
{
  JDK_VERSION = "1.7";
  LOOKAHEAD = 1;
  STATIC = false;
}

PARSER_BEGIN(InproceedingsParser)
package cn.ac.ios.iscasmc.papersprojects.backend.parser.inproceedings;
import cn.ac.ios.iscasmc.papersprojects.backend.parser.UtilParser;
import cn.ac.ios.iscasmc.papersprojects.backend.bean.PaperBean;

public final class InproceedingsParser {
	private boolean removeMarkers;

	public PaperBean parseInproceedings(boolean removeMarkers) {
		this.removeMarkers = removeMarkers;
		try {
			return actualParser();
		} catch (ParseException pe) {
			return null;
		}
	}
}

PARSER_END(InproceedingsParser)

TOKEN_MGR_DECLS : {
	int nestingLevel;
}

<*> SKIP :
{
	" "
|	"\r"
|	"\t"
|	"\n"
}

TOKEN :
{
	< INPROCEEDINGS : "@inproceedings" >
|	< BRACKET_OPEN : "{" >
|	< COMMA : "," > : BIBENTRY_ENV
|	< IDENTIFIER : ([ "A"-"Z","a"-"z",":","/","0"-"9","-" ])+ >
}

< BIBENTRY_ENV > TOKEN :
{ 
	< BE_BRACKET_OPEN : "{" > { nestingLevel = 0; } : ENTRY_ENV 
|	< BE_BRACKET_CLOSE : "}" > : DEFAULT 
|	< BE_AUTHOR : "author" >
|	< BE_TITLE : "title" >
|	< BE_BOOKTITLE : "booktitle" >
|	< BE_PAGES : "pages" >
|	< BE_YEAR : "year" >
|	< BE_CROSSREF : "crossref" >
|	< BE_URL : "url" >
|	< BE_DOI : "doi" >
|	< BE_OTHER : (["a"-"z"])+ >
|	< BE_COMMA : "," >
|	< BE_EQUAL : "=" >
}

< ENTRY_ENV > TOKEN :
{
	< E_BRACKET_OPEN : "{" > { nestingLevel++; } 
|	< E_BRACKET_CLOSE : "}" > 
|	< E_TEXT : ( ~["{","}"])+ >
}

TOKEN :
{
	< UNEXPECTED_CHAR : ~[] >
}

private PaperBean actualParser() :
{
	Token t;
	PaperBean pb = new PaperBean();
	String stringValue;
}
{
	< INPROCEEDINGS >
	< BRACKET_OPEN >
	t = < IDENTIFIER > { pb.setIdentifier(t.toString()); }
	(
	    (
		      < COMMA >
	    |
		      < BE_COMMA >
	  	)
	  	(
			< BE_AUTHOR >
			< BE_EQUAL >
			stringValue = parseStringValue()
			{
				pb.setAuthorsFromBibtex(stringValue);
			}
		|
			< BE_TITLE >
			< BE_EQUAL >
			stringValue = parseStringValue()
			{
				pb.setTitle(stringValue);
			}
		|
			< BE_BOOKTITLE >
			< BE_EQUAL >
			stringValue = parseStringValue()
			{
				pb.setBooktitle(stringValue);
			}
		|
			< BE_PAGES >
			< BE_EQUAL >
			stringValue = parseStringValue()
			{
				pb.setPages(stringValue);
			}
		|
			< BE_YEAR >
			< BE_EQUAL >
			stringValue = parseStringValue()
			{
				pb.setYear(stringValue);
			}
		|
			< BE_CROSSREF >
			< BE_EQUAL >
			stringValue = parseStringValue()
			{
				pb.setCrossref(stringValue);
			}
		|
			< BE_URL >
			< BE_EQUAL >
			stringValue = parseStringValue()
			{
				pb.setUrl(stringValue);
			}
		|
			< BE_DOI >
			< BE_EQUAL >
			stringValue = parseStringValue()
			{
				pb.setDoi(stringValue);
			}
		|
			< BE_OTHER >
			< BE_EQUAL >
			stringValue = parseStringValue()
		)
	)+
	< BE_BRACKET_CLOSE >
	<EOF>
	{
		return pb;
	}
}

private String parseStringValue() :
{
	Token t;
	StringBuilder sb = new StringBuilder();
}
{
	< BE_BRACKET_OPEN >
	(
		t = < E_TEXT > { sb.append(t.toString()); }
	|
		t = < E_BRACKET_OPEN >
		{
			sb.append(t.toString());
		}
	|
		LOOKAHEAD( { (token_source.nestingLevel > 0) && (getToken(1).kind == E_BRACKET_CLOSE) })
		t = < E_BRACKET_CLOSE >
		{
			token_source.nestingLevel--;
			sb.append(t.toString());
		}
	)+
	t = < E_BRACKET_CLOSE >
	{
		token_source.SwitchTo(BIBENTRY_ENV);
		return UtilParser.toTrimmedSingleLine(sb.toString(), removeMarkers);
	}
}
